apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: "$rootDir/dependencies.gradle"

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.veskoiliev.codewars"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.veskoiliev.TestInstrumentationRunner"
        buildConfigField 'String', 'API_BASE_URL', apiBaseUrl
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        test {
            java.srcDir "src/testCommon/java"
        }
        androidTest {
            java.srcDir "src/testCommon/java"
        }
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$app_compat_version"
    implementation "com.android.support:recyclerview-v7:$app_compat_version"
    implementation "com.android.support:design:$app_compat_version"
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"
    implementation 'com.android.support:support-vector-drawable:27.1.1'

    // Architecture components
    implementation(
            "android.arch.lifecycle:extensions:$arch_components_version",
            "android.arch.persistence.room:runtime:$arch_components_version",
            "android.arch.persistence.room:rxjava2:$arch_components_version",
            "android.arch.paging:runtime:$paging_version"
    )
    kapt "android.arch.persistence.room:compiler:$arch_components_version"

    //  Retrofit
    implementation(
            "com.squareup.retrofit2:retrofit:$retrofit_version",
            "com.squareup.retrofit2:converter-gson:$retrofit_version",
            "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    )

    // OkHttp logging
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_logging_version"

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:$rx_java_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rx_android_version"

    // Dagger
    implementation("com.google.dagger:dagger:$dagger_version",
            "com.google.dagger:dagger-android:$dagger_version",
            "com.google.dagger:dagger-android-support:$dagger_version"
    )
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    testImplementation(
            "junit:junit:$junit_version",
            "org.hamcrest:hamcrest-all:$hamcrest_version",
            "org.mockito:mockito-core:$mockito_version",
            "android.arch.core:core-testing:$arch_components_version"
    )

    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$mock_server_version"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"
}
